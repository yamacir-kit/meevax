(define-library (srfi 0)
  (import (meevax macro-transformer)
          (meevax library)
          (meevax version)
          (scheme r5rs))

  (export cond-expand)

  (begin (define (every f xs)
           (if (pair? xs)
               (and (f (car xs))
                    (every f (cdr xs)))
               #t))

         (define (any f xs)
           (if (pair? xs)
               (or (f (car xs))
                   (any f (cdr xs)))
               #f))

         (define-syntax cond-expand
           (er-macro-transformer
             (lambda (form rename compare)
               (define (match? feature-request)
                 (if (pair? feature-request)
                     (or (and (compare (rename 'library)
                                       (car feature-request))
                              (member (cadr feature-request)
                                      (libraries)))
                         (and (compare (rename 'and)
                                       (car feature-request))
                              (every match? (cdr feature-request)))
                         (and (compare (rename 'or)
                                       (car feature-request))
                              (any match? (cdr feature-request)))
                         (and (compare (rename 'not)
                                       (car feature-request))
                              (not (match? (cadr feature-request)))))
                     (or (compare (rename 'else) feature-request)
                         (memq feature-request (features)))))
               (define (expand clauses)
                 (cond ((not (pair? clauses))
                        (error "No matching clauses were found" form))
                       ((not (pair? (car clauses)))
                        (error "A <ce-clause> takes the form: (<feature requirement> <expression> ...)" (car clauses)))
                       ((match? (caar clauses))
                        `(,(rename 'begin) ,@(cdar clauses)))
                       (else (expand (cdr clauses)))))
               (expand (cdr form)))))))
